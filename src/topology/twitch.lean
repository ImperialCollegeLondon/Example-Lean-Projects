-- topological spaces from first princples!

-- Turns out there's quite a lot to it, but it's all straightforward

-- I'll start on the hour. I'll do a brief review of last week
-- (the below file, 
-- https://github.com/ImperialCollegeLondon/Example-Lean-Projects/blob/master/src/topology/twitch.lean
-- and then I'll start on the proof that the continuous image of compact is compact.

import tactic

-- remember : in Lean, `set X` means the type of subsets of X
-- or, the type of "sets of elements of X"

open set

/-- The definition of a topological space -/
class topological_space (X : Type) :=
 -- some subsets of X are called "open sets"
(is_open : set X â†’ Prop)
 -- X itself is open
(is_open_univ : is_open univ)
 -- intersection of two open sets is open
(is_open_inter : âˆ€ U V : set X, is_open U â†’ is_open V â†’ is_open (U âˆ© V))
-- arbitrary union of open sets is open
(is_open_sUnion : âˆ€ (ğ’ : set (set X)), (âˆ€ U âˆˆ ğ’, is_open U) â†’ is_open (â‹ƒâ‚€ ğ’))

-- what is an "arbitrary union of open sets"?
-- I've set it up as a set of open sets
-- but you might have an "indexed family of open sets"
-- ie some type Î¹, and for all  i âˆˆ Î¹ an open set U_i
-- and you want â‹ƒ U_i open

namespace topological_space

-- let X be a topological space

variables {X : Type} [topological_space X]

-- let's do indexed unions

lemma is_open_Union {Î¹ : Type} {f : Î¹ â†’ set X} (hf : âˆ€ i : Î¹, is_open (f i)) :
  is_open (â‹ƒ i, f i) :=
begin
  apply is_open_sUnion,
  intros U hU,
  cases hU with i hi,
  dsimp at hi,
  rw â†hi,
  apply hf,
end

-- empty set is open
lemma is_open_empty : is_open (âˆ… : set X) :=
begin
  let ğ’ : set (set X) := âˆ…,
  have hğ’ : âˆ€ U âˆˆ ğ’, is_open U,
  { rintro U âŸ¨âŸ©,
  },
  convert is_open_sUnion ğ’ hğ’,
  rw sUnion_empty,
end

-- finite intersection of open sets is open
-- proof by induction on size of finite set
lemma is_open_sInter {ğ’ : set (set X)} (hğ’ : finite ğ’) :
  (âˆ€ U âˆˆ ğ’, is_open U) â†’ is_open â‹‚â‚€ ğ’ :=
begin
  apply finite.induction_on hğ’,
  { -- base case,
    intros,
    convert is_open_univ,
    rw sInter_empty },
  { -- inductive step
    -- going to use is_open_inter
    intro U,
    intro ğ’,
    intro hUğ’,
    intro hğ’,
    intro hğ’2,
    -- h says "assume both U and every element of ğ’ is open"
    -- insert U ğ’ means {U} âˆª ğ’
    intro h,
    rw sInter_insert,
    apply is_open_inter,
    { apply h,
      simp },
    { apply hğ’2,
      intros U hU,
      apply h,
      simp [hU] }},
end

def is_closed (C : set X) : Prop := is_open Cá¶œ

@[simp] lemma is_closed_iff (C : set X) : is_closed C â†” is_open Cá¶œ := iff.rfl

-- clearly could spend all day proving facts about closed sets now

lemma is_closed_empty : is_closed (âˆ… : set X) :=
begin
  simp [is_open_univ],
end

end topological_space

-- next : continuous functions

open topological_space

variables {X : Type} [topological_space X]
  {Y : Type} [topological_space Y]

/-- a function X â†’ Y between topological spaces is continuous if the
  preimage of every open set is open -/
def continuous (f : X â†’ Y) : Prop :=
âˆ€ U, is_open U â†’ is_open (fâ»Â¹' U)

theorem continuous_id : continuous (id : X â†’ X) :=
begin
  intro U,
  intro hU,
  -- interesting question
  -- clearly idâ»Â¹' U = U
  -- But this is true *by definition*?
  -- another interesting question
  -- clearly id'' U = U (pushforward)
  -- but is this true *by definition*?
  -- THESE QUESTIONS ARE NOT MATHEMATICAL QUESTIONS
  -- They depend not on the specification, but on the *implementation*
--   have h1 : U = id '' U,
--   { --refl, -- fails!
--     -- not true by definition
--     ext x,
--     split,
--       intro h,
--       unfold set.image,
--       use x, -- this is why it's not true by definition
--       split, assumption, refl,
--       rintro âŸ¨y, hy1, rflâŸ©,
--       exact hy1,
--   },
--   have h2 : U = idâ»Â¹' U, -- true by definition
--     refl,
  exact hU,
end

variables {Z : Type} [topological_space Z]

theorem continuous.comp {f : X â†’ Y} {g : Y â†’ Z} (hf : continuous f)
  (hg : continuous g) : continuous (g âˆ˜ f) :=
begin
  intro U,
  intro hU,
  change is_open ((g âˆ˜ f)â»Â¹' U),
  change is_open (fâ»Â¹' (gâ»Â¹' U)),
  -- proving it backwards
  apply hf,
  apply hg,
  exact hU
end

-- term mode proof (same proof!)
theorem continuous.comp' {f : X â†’ Y} {g : Y â†’ Z} (hf : continuous f)
  (hg : continuous g) : continuous (g âˆ˜ f) :=
Î» U hU, hf (gâ»Â¹' U) (hg _ hU)

/-- a subset C of a top space X is compact if every open cover has a finite subcover -/
def compact (C : set X) : Prop :=
  âˆ€ (Î¹ : Type) (U : Î¹ â†’ set X) (hi : âˆ€ i : Î¹, is_open (U i)) (hC : C âŠ† â‹ƒi, U i),
  âˆƒ F : set Î¹, finite F âˆ§ C âŠ† â‹ƒ i âˆˆ F, U i

-- this definition seems to me to be easier to work with

def hausdorff (X : Type) [topological_space X] : Prop :=
âˆ€ x y : X, x â‰  y â†’ âˆƒ U V : set X, is_open U âˆ§ is_open V âˆ§ x âˆˆ U âˆ§ y âˆˆ V âˆ§ U âˆ© V = âˆ…

-- Theorem: continuous image of a compact set is compact
theorem compact_map {f : X â†’ Y} (hf : continuous f) {C : set X} (hC : compact C) :
  compact (f '' C) :=
begin
  -- suffices to prove that if f(C) is covered by open sets, it has a 
  -- finite subcover
  intros I U hU hUC,
  -- hUC : f(C) âŠ† â‹ƒ_{i âˆˆ I} Uáµ¢
  -- So say we've covered f(C) by open sets
  -- Then C has a cover by open sets, namely Váµ¢ := fâ»Â¹(Uáµ¢),
  let V : I â†’ set X := Î» i, fâ»Â¹' (U i),
  -- Let's check that all the Váµ¢ are open
  have hV : âˆ€ i : I, is_open (V i),
  { intro i,
    apply hf,
    apply hU },
  -- Let's check that the Váµ¢ cover C
  have hVC : C âŠ† â‹ƒ i, V i,
  { -- say x âˆˆ C,
    intro x,
    intro hx,
    -- then f(x) âˆˆ â‹ƒ_i Uáµ¢
    have hx2 : f x âˆˆ â‹ƒ i, U i,
      apply hUC,
      use [x, hx],
    -- f(x) âˆˆ â‹ƒ_i Uáµ¢, so âˆƒ i s.t. f(x) âˆˆ Uáµ¢
    rw mem_Union at hx2 âŠ¢,
    cases hx2 with i hi,
    use i,
    exact hi },
  -- but C is compact
  specialize hC I V hV hVC,
  -- so there exists a finite subcover of Váµ¢,
  rcases hC with âŸ¨F, hF, hFCâŸ©,
  -- I claim that corresponding Uáµ¢ will work
  use [F, hF],
  -- Let's check they cover f(C),
  rintros _ âŸ¨x, hx1, rflâŸ©,
  specialize hFC hx1,
  rw mem_bUnion_iff at hFC âŠ¢,
  exact hFC,
  -- They do, so the cover of f(C) had a finite subcover :D
end

-- To prove that a compact subspace of a Hausdorff space is closed,
-- we need the fact that a "locally open" set is open!

-- So let's prove that first

lemma open_iff_locally_open (V : set X) :
  is_open V â†” âˆ€ x : X, x âˆˆ V â†’ âˆƒ U : set X, x âˆˆ U âˆ§ is_open U âˆ§ U âŠ† V :=
âŸ¨Î» hV x hx, âŸ¨V, hx, hV, subset.refl _âŸ©, Î» h, begin
  let ğ’ : set (set X) := {U : set X | is_open U âˆ§ U âŠ† V},
    -- ğ’ doesn't just contain the neighbourhoods of x for each x âˆˆ V
    -- ğ’ contains more sets, e.g. the empty set!
    -- Clearly every set in ğ’ is open, so their union is open
    convert is_open_sUnion ğ’ _,
    swap,
    { intros U H, cases H, assumption},
    -- It suffices to prove that V is the union of the elements of ğ’
    { ext x,
      split,
      -- let's prove inclusions in both directions
      { intro hx,
        rcases h x hx with âŸ¨U, hU1, hU2, hU3âŸ©,
        rw mem_sUnion,
        use U,
        use hU2,
        exact hU3,
        exact hU1 },
      { -- easy way
        intro hx,
        rw mem_sUnion at hx,
        rcases hx with âŸ¨U, hUC, hxUâŸ©,
        cases hUC with h1 h2,
        apply h2 hxU }}
endâŸ©
-- #exit
-- begin
--   split,
--   { -- This way is easy. Say V is open.
--     intro hV,
--     -- say x âˆˆ V
--     intros x hx,
--     -- Want an open neighbourhood of x contained in V
--     -- let's just use V :-)
--     use V,
--     use hx,
--     use hV }, -- last goal V âŠ† V closed automatically by `refl`,
--   { intro h,
--     -- Reid Barton trick!
--     let ğ’ : set (set X) := {U : set X | is_open U âˆ§ U âŠ† V},
--     -- ğ’ doesn't just contain the neighbourhoods of x for each x âˆˆ V
--     -- ğ’ contains more sets, e.g. the empty set!
--     -- Clearly every set in ğ’ is open, so their union is open
--     convert is_open_sUnion ğ’ _,
--     swap,
--     { tidy },
--     -- It suffices to prove that V is the union of the elements of ğ’
--     { ext x,
--       split,
--       -- let's prove inclusions in both directions
--       { intro hx,
--         rcases h x hx with âŸ¨U, hU1, hU2, hU3âŸ©,
--         rw mem_sUnion,
--         use U,
--         use hU2,
--         exact hU3,
--         exact hU1 },
--       { -- easy way
--         intro hx,
--         rw mem_sUnion at hx,
--         rcases hx with âŸ¨U, hUC, hxUâŸ©,
--         cases hUC with h1 h2,
--         apply h2 hxU }}}
-- end

-- prove this next Tues
theorem is_closed_of_compact (hX : hausdorff X) {C : set X} (hC : compact C) : is_closed C :=
begin
  unfold is_closed,
  -- let's start with the maths proof
  -- We're going to prove that Cá¶œ is open by showing it's locally open
  -- Let x âˆˆ Cá¶œ i.e. x : X and x âˆ‰ C
  -- If we can find an open subset U âŠ† Cá¶œ with x âˆˆ U then we're done
  -- by the previous lemma
  rw open_iff_locally_open,
  intros x hx,
  -- Where do we find such U?
  -- Now is where we use compactness.
  -- We're going to cover C by a bunch of open sets
  -- Where do we get the open sets?
  -- We get them from Hausdorffness
  -- Say y âˆˆ C
  -- Then x â‰  y because x âˆ‰ C
  -- so by Hausdorff, x âˆˆ Vâ‚ and y âˆˆ Vâ‚‚ and Vâ‚ âˆ© Vâ‚‚ = âˆ…
  -- In particular x âˆ‰ Vâ‚‚ = Vâ‚‚(y)
  -- The union of the Vâ‚‚(y) covers C because y âˆˆ C was arbitrary and y âˆˆ Vâ‚‚(y)
  -- So there's a finite subcover
  -- Now take the intersection of the corresponding Vâ‚(y)
  -- finite intersection of open sets is open
  -- and completely misses C
  
end


